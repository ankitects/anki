load("@npm//@bazel/typescript:index.bzl", "ts_library")
load("@npm//@bazel/rollup:index.bzl", "rollup_bundle")
load("//ts:svelte.bzl", "compile_svelte", "svelte_check")
load("//ts:prettier.bzl", "prettier_test")
load("//ts:eslint.bzl", "eslint_test")
load("@io_bazel_rules_sass//:defs.bzl", "sass_binary")

sass_binary(
    name = "graphs_css",
    src = "graphs.scss",
    visibility = ["//visibility:public"],
    deps = [
        "//ts/sass:core_lib",
    ],
)

svelte_files = glob(["*.svelte"])

svelte_names = [f.replace(".svelte", "") for f in svelte_files]

compile_svelte(
    name = "svelte",
    srcs = svelte_files,
)

ts_library(
    name = "bootstrap",
    srcs = ["bootstrap.ts"],
    deps = [
        "GraphsPage",
        "lib",
        "//ts/lib",
        "@npm//svelte",
        "@npm//svelte2tsx",
    ],
)

ts_library(
    name = "types",
    srcs = ["d3_missing.d.ts"],
)

ts_library(
    name = "lib",
    srcs = glob(
        ["*.ts"],
        exclude = ["bootstrap.ts"],
    ),
    deps = [
        "types",
        "//ts/lib",
        "//ts/lib:backend_proto",
        "@npm//@types/d3-array",
        "@npm//@types/d3-axis",
        "@npm//@types/d3-interpolate",
        "@npm//@types/d3-scale",
        "@npm//@types/d3-scale-chromatic",
        "@npm//@types/d3-selection",
        "@npm//@types/d3-shape",
        "@npm//@types/d3-time",
        "@npm//@types/d3-transition",
        "@npm//@types/lodash",
        "@npm//d3-array",
        "@npm//d3-axis",
        "@npm//d3-interpolate",
        "@npm//d3-scale",
        "@npm//d3-scale-chromatic",
        "@npm//d3-selection",
        "@npm//d3-shape",
        "@npm//d3-time",
        "@npm//d3-transition",
        "@npm//lodash.debounce",
        "@npm//lodash.throttle",
    ],
)

rollup_bundle(
    name = "graphs",
    srcs = [
        "//ts/lib:es6",
    ],
    config_file = "//ts:rollup.config.js",
    entry_point = "bootstrap.ts",
    format = "iife",
    link_workspace_root = True,
    silent = True,
    sourcemap = "false",
    visibility = ["//visibility:public"],
    deps = [
        "bootstrap",
        "@npm//@rollup/plugin-commonjs",
        "@npm//@rollup/plugin-node-resolve",
        "@npm//rollup-plugin-terser",
    ] + svelte_names,
)

exports_files(["graphs.html"])

# Tests
################

prettier_test(
    name = "format_check",
    srcs = glob([
        "*.ts",
        "*.svelte",
    ]),
)

eslint_test(
    name = "eslint",
    srcs = glob(
        [
            "*.ts",
        ],
        exclude = ["d3_missing.d.ts"],
    ),
)

svelte_check(
    name = "svelte_check",
    srcs = glob([
        "*.ts",
        "*.svelte",
    ]),
)
