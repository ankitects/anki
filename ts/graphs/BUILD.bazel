load("@npm//@bazel/typescript:index.bzl", "ts_library")
load("@npm//@bazel/rollup:index.bzl", "rollup_bundle")
load("//ts:svelte.bzl", "compile_svelte", "svelte_check")
load("//ts:prettier.bzl", "prettier_test")
load("//ts:eslint.bzl", "eslint_test")
load("@io_bazel_rules_sass//:defs.bzl", "sass_binary")

sass_binary(
    name = "graphs_css",
    src = "graphs.scss",
    visibility = ["//visibility:public"],
    deps = [
        "//ts/sass:core_lib",
    ],
)

svelte_files = glob(["*.svelte"])

svelte_names = [f.replace(".svelte", "") for f in svelte_files]

compile_svelte(
    name = "svelte",
    srcs = svelte_files,
)

ts_library(
    name = "index",
    srcs = ["index.ts"],
    deps = [
        "GraphsPage",
        "lib",
        "//ts/lib",
        "@npm//svelte",
        "@npm//svelte2tsx",
    ],
)

ts_library(
    name = "lib",
    srcs = glob(
        ["*.ts"],
        exclude = ["index.ts"],
    ),
    deps = [
        "//ts/lib",
        "//ts/lib:backend_proto",
        "@npm//@types/d3",
        "@npm//@types/lodash",
        "@npm//d3",
        "@npm//lodash.debounce",
        "@npm//lodash.throttle",
        "@npm//svelte",
        "@npm//@emotion/css",
    ],
)

rollup_bundle(
    name = "graphs",
    config_file = "//ts:rollup.config.js",
    entry_point = "index.ts",
    format = "iife",
    link_workspace_root = True,
    silent = True,
    sourcemap = "false",
    visibility = ["//visibility:public"],
    deps = [
        "//ts/lib",
        "index",
        "@npm//@rollup/plugin-babel",
        "@npm//@rollup/plugin-commonjs",
        "@npm//@rollup/plugin-node-resolve",
        "@npm//rollup-plugin-terser",
        "@npm//babel-plugin-transform-inline-environment-variables",
    ] + svelte_names,
)

# Tests
################

prettier_test(
    name = "format_check",
    srcs = glob([
        "*.ts",
        "*.svelte",
    ]),
)

eslint_test(
    name = "eslint",
    srcs = glob(
        [
            "*.ts",
        ],
    ),
)

svelte_check(
    name = "svelte_check",
    srcs = glob([
        "*.ts",
        "*.svelte",
    ]),
)
