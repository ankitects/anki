load("@npm//@bazel/typescript:index.bzl", "ts_library")
load("//ts/svelte:svelte.bzl", "compile_svelte", "svelte_check")
load("//ts:prettier.bzl", "prettier_test")
load("//ts:eslint.bzl", "eslint_test")
load("//ts:esbuild.bzl", "esbuild")
load("//ts:compile_sass.bzl", "compile_sass")

compile_sass(
    group = "base_css",
    srcs = ["graphs-base.scss"],
    deps = [
        "//ts/sass:base_lib",
        "//ts/sass:scrollbar_lib",
    ],
    visibility = ["//visibility:public"],
)

svelte_files = glob(["*.svelte"])

svelte_names = [f.replace(".svelte", "") for f in svelte_files]

compile_svelte(
    name = "svelte",
    srcs = svelte_files,
)

ts_library(
    name = "index",
    srcs = ["index.ts"],
    deps = [
        "GraphsPage",
        "lib",
        "//ts/lib",
        "//ts/sveltelib",
        "@npm//svelte",
        "@npm//svelte2tsx",
    ],
)

ts_library(
    name = "lib",
    srcs = glob(
        ["*.ts"],
        exclude = ["index.ts"],
    ),
    deps = [
        "//ts/lib",
        "//ts/lib:backend_proto",
        "//ts/sveltelib",
        "@npm//@types/d3",
        "@npm//@types/lodash",
        "@npm//d3",
        "@npm//lodash.debounce",
        "@npm//lodash.throttle",
        "@npm//svelte",
    ],
)

esbuild(
    name = "graphs",
    srcs = [
        "//ts:protobuf-shim.js",
    ],
    args = [
        "--global-name=anki",
        "--inject:$(location //ts:protobuf-shim.js)",
        "--resolve-extensions=.mjs,.js",
        "--log-level=warning",
    ],
    entry_point = "index.ts",
    external = [
        "protobufjs/light",
    ],
    output_css = True,
    visibility = ["//visibility:public"],
    deps = [
        "//ts/sveltelib",
        "//ts/lib",
        "//ts/lib:backend_proto",
        ":index",
        ":base_css",
    ] + svelte_names,
)

exports_files(["graphs.html"])

# Tests
################

prettier_test(
    name = "format_check",
    srcs = glob([
        "*.ts",
        "*.svelte",
    ]),
)

eslint_test(
    name = "eslint",
    srcs = glob(
        [
            "*.ts",
        ],
    ),
)

svelte_check(
    name = "svelte_check",
    srcs = glob([
        "*.ts",
        "*.svelte",
    ]),
)
